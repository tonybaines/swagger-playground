buildscript {
    ext.kotlin_version = '1.1.51'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
plugins {
    id "io.vertx.vertx-plugin" version "0.0.5"
    id 'org.hidetake.swagger.generator' version '2.8.0'
}
apply plugin: 'kotlin'

group 'github.tonybaines'
version '0.1.0-SNAPSHOT'

repositories {
    mavenCentral()
    jcenter()
    maven {
        url "https://jitpack.io"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile 'io.vertx:vertx-core:3.5.0'
    compile 'io.vertx:vertx-web:3.5.0'
    compile 'io.vertx:vertx-web-api-contract:3.5.0'
    compile 'com.google.code.gson:gson:2.8.2'
    // Add dependency for Swagger UI
    swaggerUI 'org.webjars:swagger-ui:2.2.6'
    // Add dependency for Swagger Codegen CLI
    swaggerCodegen 'io.swagger:swagger-codegen-cli:2.2.3'
}

vertx {
    mainVerticle = 'Swagger'
}
def swaggerDef = file('src/main/resources/swagger-api-v1.json')

swaggerSources {
    docs {
        inputFile = swaggerDef
        ui { options.docExpansion = 'full' }
        code { language = 'html2' }
    }
    python {
        inputFile = swaggerDef
        code { language = 'python' }
    }
    java {
        inputFile = swaggerDef
        code { language = 'java' }
    }
    kotlin {
        inputFile = swaggerDef
        code { language = 'kotlin' }
    }
    vertx {
        inputFile = swaggerDef
        code { language = 'java-vertx' }
    }
    bash {
        inputFile = swaggerDef
        code { language = 'bash' }
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}